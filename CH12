libname data "/home/hih950031/SASProgramming";

/*Frequency*/
PROC FREQ DATA=data.mortgage;
	TABLES default_time;
RUN;

/*データセット情報の表示*/
PROC CONTENTS DATA=data.mortgage; 
RUN;

ODS GRAPHICS ON;
PROC UNIVARIATE DATA=data.mortgage;
VAR FICO_orig_time LTV_orig_time;
CDFPLOT FICO_orig_time LTV_orig_time;
HISTOGRAM FICO_orig_time LTV_orig_time;
RUN;
ODS GRAPHICS OFF;

/*Location measure*/
PROC MEANS DATA=data.mortgage
N MEAN MEDIAN MODE P1 P99 MAXDEC=4;
VAR default_time FICO_orig_time LTV_orig_time;
RUN;

ODS GRAPHICS ON;
PROC UNIVARIATE DATA=data.mortgage NOPRINT;
QQPLOT default_time FICO_orig_time LTV_orig_time / NORMAL(MU=EST SIGMA=EST COLOR=LTGREY) ;
RUN;
ODS GRAPHICS OFF;

/*Dispersion measure*/
PROC MEANS DATA=data.mortgage
N MIN MAX RANGE QRANGE VAR STD CV MAXDEC=4;
VAR default_time FICO_orig_time LTV_orig_time;
RUN;

/*Skewness and Kurtosis measure*/
PROC MEANS DATA=data.mortgage
N SKEW KURT MAXDEC=4;
VAR default_time FICO_orig_time LTV_orig_time;
RUN;

/*Two-dimentional contingency table*/
DATA mortgage1;
SET data.mortgage;
RUN;
PROC SORT DATA = mortgage1;
BY id time;
RUN;
PROC RANK DATA = mortgage1
GROUPS=5
OUT=quint(KEEP=id time FICO_orig_time);
VAR FICO_orig_time;
RUN;
DATA new;
MERGE mortgage1 quint;
BY id time;
RUN;
PROC FREQ DATA=new ;
TABLES default_time*FICO_ORIG_TIME;
RUN;

/*box plot*/
PROC SORT DATA= mortgage1;
BY default_time;
RUN;

ODS GRAPHICS ON;
PROC BOXPLOT DATA = mortgage1;
PLOT FICO_orig_time*default_time /IDSYMBOL=CIRCLE
IDHEIGHT=2 CBOXES=BLACK BOXWIDTH=10 ;
RUN;
ODS GRAPHICS OFF;

ODS GRAPHICS ON;
PROC BOXPLOT DATA = mortgage1;
PLOT LTV_orig_time*default_time / IDSYMBOL=CIRCLE
IDHEIGHT=2 CBOXES=BLACK BOXWIDTH=10 ;
RUN;
ODS GRAPHICS OFF;

/*Correlation measures*/
PROC FREQ DATA=new;
TABLES default_time*FICO_ORIG_TIME / CHISQ;
RUN;

DATA sample;
SET data.mortgage;
IF RANUNI(123456) < 0.01;
RUN;

ODS GRAPHICS ON;
PROC CORR DATA=sample
PLOTS(MAXPOINTS=NONE)=SCATTER(NVAR=2 ALPHA=.20 .30)
KENDALL SPEARMAN;
VAR FICO_orig_time LTV_orig_time;
RUN;
ODS GRAPHICS OFF;


/*Confidence interval*/
ODS SELECT BASICINTERVALS;
PROC UNIVARIATE DATA=data.mortgage CIBASIC(ALPHA=.01);
VAR LTV_orig_time;
RUN;

/*Hypothesis testing*/
ODS GRAPHICS ON;
ODS SELECT TESTSFORLOCATION ;
PROC UNIVARIATE DATA=data.mortgage MU0=60;
VAR LTV_orig_time;
RUN;
ODS GRAPHICS OFF;

/*Stratified sampling*/
PROC SORT DATA=data.hmeq;
BY bad;
RUN;

PROC SURVEYSELECT DATA=data.hmeq
METHOD=SRS N=1000 SEED=12345 OUT=data.mySample;
STRATA bad / ALLOC=PROP;
RUN;

PROC FREQ DATA=data.hmeq;
TABLES bad;
RUN;

PROC FREQ DATA=data.mySample;
TABLES bad;
RUN;

/*VISUAL DATA EXPLORATION AND EXPLORATORY
STATISTICAL ANALYSIS*/




/*CH6*/
/*Discrete Time series model*/
libname data "/home/hih950031/SASProgramming";

/*Linear model*/
PROC REG DATA=data.mortgage;
MODEL default_time = FICO_orig_time LTV_orig_time gdp_time;
RUN;

/*Non-linear model*/
DATA graph;
DO x=-5 TO 5 BY 0.5;
logit=cdf('LOGISTIC',x);
probit=cdf('NORMAL',x);
cloglog=1-EXP(-EXP(x));
OUTPUT;
END;
RUN;

ODS GRAPHICS ON;
AXIS1 ORDER=(-5 to 5 BY 1) LABEL=('Linear predictor');
AXIS2 ORDER=(0 to 1 BY 0.25) LABEL=('Probability');
SYMBOL1 INTERPOL=SPLINE WIDTH=2 VALUE=TRIANGLE C=BLUE;
SYMBOL2 INTERPOL=SPLINE WIDTH=2 VALUE=CIRCLE C=RED;
SYMBOL3 INTERPOL=SPLINE WIDTH=2 VALUE=SQUARE C=BLACK;
LEGEND1 LABEL=NONE SHAPE=SYMBOL(4,2)
POSITION=(BOTTOM OUTSIDE);

PROC GPLOT DATA=graph; 
PLOT logit*x probit*x cloglog*x / OVERLAY LEGEND=LEGEND1 HAXIS=AXIS1 VAXIS=AXIS2;
RUN;
ODS GRAPHICS OFF;


/*Probit model*/
PROC LOGISTIC DATA=data.mortgage DESCENDING;
MODEL default_time =FICO_orig_time LTV_time gdp_time / LINK=PROBIT RSQUARE;
OUTPUT OUT=probabilities_Probit PREDICTED=PD_time;
RUN;

/*Calibration of Probit Model*/
PROC MEANS DATA=probabilities_Probit MEAN NOLABELS;
VAR default_time PD_time;
RUN;


/*Logit Model*/
PROC LOGISTIC DATA=data.mortgage DESCENDING;
MODEL default_time = FICO_orig_time LTV_time gdp_time;
OUTPUT OUT=probabilities_Logit PREDICTED=PD_time;
RUN;

/*Calibration of Logit Model*/
PROC MEANS DATA=probabilities_Logit MEAN NOLABELS;
VAR default_time PD_time;
RUN;


/*cloglog model*/
PROC LOGISTIC DATA=data.mortgage DESCENDING;
MODEL default_time = FICO_orig_time
LTV_time gdp_time/ LINK=CLOGLOG;
OUTPUT OUT=probabilities_clog PREDICTED=PD_time;
RUN;

/*Calibration of Logit Model*/
PROC MEANS DATA=probabilities_clog MEAN NOLABELS;
VAR default_time PD_time;
RUN;


data mortgage;
SET data.mortgage;
orig_time2=orig_time;
IF orig_time NOT IN (20,21,22,23,24,25) THEN orig_time2 =0;
RUN;

PROC LOGISTIC DATA=mortgage DESCENDING;
CLASS orig_time2/PARAM=REFERENCE;
MODEL default_time = FICO_orig_time
LTV_time gdp_time orig_time2 / LINK=PROBIT;
RUN;




/*CH12*/
/*Probit etimation*/
/*Data preprocessing and selection of 1 percent random sample*/
libname data "/home/hih950031/SASProgramming";

/*Data preprocessing and selection of 1 percent random sample*/
DATA sample;
SET data.mortgage;
time1 = time-first_time;
time2 = time-first_time+1;
IF RANUNI(12345) < 0.01;
RUN;

/*Probit Model with PROC LOGISTIC*/
ODS GRAPHICS ON;
PROC LOGISTIC DATA=sample DESCENDING;
MODEL default_time = FICO_orig_time LTV_orig_time gdp_time / OUTROC=roc_logistic RSQUARE LINK=Probit;
RUN;
ODS GRAPHICS OFF;

/*Probit Model with MCMC*/
/*using a diffuse prior with mean 0 and variance of 1,000*/
ODS LISTING CLOSE;
/*ODS LATEX;*/
ODS GRAPHICS ON / IMAGEFMT=PDF IMAGENAME='bayesprobit';
PROC MCMC DATA = sample Statistics=ALL Diagnostics=ALL
SCALE=5 MINTUNE=5 NBI=1000 NMC=10000 THIN=2 PROPCOV=QUANEW SEED=12345;
PARMS beta0 beta1 beta2 beta3;
PRIOR beta0 beta1 beta2 beta3 ~ normal(0, var = 1000);
pd = PROBNORM(beta0 + beta1 / 10 * FICO_orig_time + beta2 / 100 * LTV_orig_time + beta3 /100 * gdp_time);
MODEL default_time ~ binomial(1, pd);
RUN;
ODS GRAPHICS OFF;

/*Probit Model with MCMC*/
/*now specify a normal prior with mean 3 and variance 0.5*/
ODS LISTING CLOSE;
/*ODS LATEX;*/
ODS GRAPHICS ON / IMAGEFMT=PDF IMAGENAME='bayes2probit';
PROC MCMC DATA = sample STATISTICS=ALL DIAGNOSTICS=ALL
SCALE=5 MINTUNE=5 NBI=1000 NMC=10000 THIN=2 PROPCOV=QUANEW SEED=12345;
PARMS beta0 beta1 beta2 beta3;
PRIOR beta0 beta1 beta3 ~ normal(0, var = 1000);
PRIOR beta2 ~ normal (3, var = 0.5);
pd = PROBNORM(beta0 + beta1 / 10 * FICO_orig_time +
beta2 / 100 * LTV_orig_time + beta3 /100 * gdp_time);
MODEL default_time ~ BINOMIAL(1,pd);
RUN;

/*Survival Model using PROC PHREG with Bayesian approach*/
ODS GRAPHICS ON / IMAGEFMT=PDF IMAGENAME='bayessurvival';
PROC PHREG DATA=sample PLOTS=CUMHAZ PLOTS(OVERLAY)=SURVIVAL;
MODEL (Time1,Time2)*default_time(0)=FICO_orig_time
LTV_orig_time gdp_time;
BAYES SEED=1 COEFFPRIOR=NORMAL STATISTICS=ALL NMC=5000
DIAGNOSTICS=ALL PLOTS=ALL;
ID ID Time default_time;
STORE out=score_phreg;
RUN;
ODS GRAPHICS OFF;

/*Probit-Linear Regression without Covariates with MCMC*/
ODS LISTING CLOSE;
/*ODS LATEX;*/
ODS GRAPHICS ON / IMAGEFMT=PDF IMAGENAME='bayescorr';
PROC MCMC DATA = sample Statistics=ALL Diagnostics=ALL
SCALE=5 MINTUNE=5 NBI=1000 NMC=10000 THIN=2 PROPCOV=QUANEW SEED=12345;
PARMS beta0 0 sigma2 0.1;
PRIOR beta0 ~ normal(0, var = 10000);
PRIOR sigma2 ~ Uniform(0, 1);
mu = beta0;
MODEL probit_dr ~ n(mu, var = sigma2);
RUN;
ODS GRAPHICS OFF;

/*Probit-Linear Regression without Covariates with MCMC*/
ODS LISTING CLOSE;
ODS GRAPHICS ON / IMAGEFMT=PDF IMAGENAME='bayes2corr';
PROC MCMC DATA = sample Statistics=ALL Diagnostics=ALL Scale=5
MINTUNE=5 NBI=1000 NMC=10000 THIN=2 PROPCOV=QUANEW SEED=12345;
PARMS beta0 0 sigma2 0.1;
PRIOR beta0 ~ normal(0, var = 10000);
PRIOR sigma2 ~ lognormal(0.1, var = 0.1);
mu = beta0;
MODEL probit_dr ~ n(mu, var = sigma2);
RUN;
ODS GRAPHICS OFF;


